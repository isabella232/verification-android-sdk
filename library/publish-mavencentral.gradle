apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = 'com.sinch.android.sdk.verification'
version = bintrayVersion

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

afterEvaluate {
    publishing {
        publications {
            Production(MavenPublication) {
                from components.productionRelease
                artifact androidSourcesJar

                groupId = 'com.sinch.android.sdk.verification'
                artifactId = bintrayName
                version = bintrayVersion

                pom {
                    name = pomName
                    description = pomDescription
                    url = 'https://www.sinch.com/products/apis/verification/'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'alewoj'
                            name = 'Aleksander WÃ³jcik'
                            email = 'aleksander.wojcik@sinch.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:sinch/verification-android-sdk.git'
                        developerConnection = 'scm:git:git@github.com:sinch/verification-android-sdk.git'
                        url = 'https://github.com/sinch/verification-android-sdk'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = 'com.sinch.android.sdk.verification'
    stagingProfileId = sonatypeStagingProfileId
    username = ossrhUsername
    password = ossrhPassword
}

signing {
    sign publishing.publications
}