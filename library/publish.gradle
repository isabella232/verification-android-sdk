apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    override = true
    pkg {
        repo = 'com.sinch.android.sdk.verification'
        name = bintrayName
        userOrg = 'sinch'
        publicDownloadNumbers = true
        version {
            name = bintrayVersion
            desc = "Version ${bintrayVersion}"
            released = new Date()
            vcsTag = bintrayVersion
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            Production(MavenPublication) {
                from components.productionRelease

                groupId = 'com.sinch.android.sdk.verification'
                artifactId = bintrayName
                version = bintrayVersion

                pom.withXml {
                    def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}