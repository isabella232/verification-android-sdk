//Common gradle build file for all android libraries.
subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply from: "$project.rootDir/signing.gradle"
    apply from: "$project.rootDir/apienv.gradle"

    android {

        compileSdkVersion(Versions.compileSdk)
        buildToolsVersion "$Versions.buildTools"

        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }

        defaultConfig {
            minSdkVersion(Versions.minSdk)
            targetSdkVersion(Versions.targetSdk)
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            consumerProguardFiles 'consumer-rules.pro', rootProject.file("common-lib-consumer-rules.pro")
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${Versions.kotlin}"
        implementation "androidx.appcompat:appcompat:${Versions.appCompat}"
        implementation "androidx.core:core-ktx:${Versions.ktx}"
        implementation "androidx.annotation:annotation:${Versions.appCompat}"

        testImplementation "junit:junit:${Versions.jUnit}"
        testImplementation "org.robolectric:robolectric:${Versions.robolectric}"
        testImplementation "androidx.test:core:${Versions.androidXTestCore}"
        testImplementation "io.mockk:mockk:${Versions.mockk}"
        testImplementation "com.squareup.retrofit2:retrofit-mock:${Versions.retrofit}"

        androidTestImplementation "androidx.test.ext:junit:${Versions.jUnitAndroidX}"
        androidTestImplementation "androidx.test.espresso:espresso-core:${Versions.espresso}"
    }
}